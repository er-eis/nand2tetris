// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
	Mux16(a=x, b=false, sel=zx, out=zxOutput);
	
	Not16(in=zxOutput, out=negatedZXOutput);
	Mux16(a=zxOutput, b=negatedZXOutput, sel=nx, out=nxOutput);
	
	Mux16(a=y, b=false, sel=zy, out=zyOutput);
	
	Not16(in=zyOutput, out=negatedZYOutput);
	Mux16(a=zyOutput, b=negatedZYOutput, sel=ny, out=nyOutput);
	
	Add16(a=nxOutput, b=nyOutput, out=nxPlusNy);
	And16(a=nxOutput, b=nyOutput, out=nxAndNy);
	Mux16(a=nxAndNy, b=nxPlusNy, sel=f, out=fOutput);
	
	Not16(in=fOutput, out=negatedFOutput);
	Mux16(a=fOutput, b=negatedFOutput, sel=no, out=out);
	Mux16(a=fOutput, b=negatedFOutput, sel=no, out[0..7]=firstInternalOut, out[8..14]=secondInternalOut, out[15]=signedBit);
	
	Or8Way(in=firstInternalOut, out=orFirst8OutBits);
	Or8Way(in[0..6]=secondInternalOut, in[7]=signedBit, out=orLast8OutBits);
	Or(a=orFirst8OutBits, b=orLast8OutBits, out=or16OutBits);
	Xor(a=or16OutBits, b=true, out=zr);
	
	And(a=signedBit, b=true, out=ng);
}